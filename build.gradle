/*
 * (C) Copyright IBM Corp. 2021
 *
 * SPDX-License-Identifier: Apache-2.0
 */

allprojects {
    apply plugin: 'scala'
    group = 'org.alvearie.hri.flink'
    version = 'Alpha.Dev'

    ext {
        //TODO: CHANGE ME BACK! (Temp replace with WHFHRI-671-SNAPSHOT branch version for testing)
        //NOTE: Using GitHub package version published on the  WHFHRI-671-SNAPSHOT branch for testing GH Actions changes
        //pipelineCoreVersion = 'develop-SNAPSHOT'
        pipelineCoreVersion = 'WHFHRI-671-SNAPSHOT'
        javaVersion = '1.8'
        flinkVersion = '1.10.0'
        scalaBinaryVersion = '2.12'
        scalaVersion = '2.12.11'
        scalaTestVersion = '3.1.1'
        jacksonVersion = '2.12.0'
        slf4jVersion = '1.7.7'
        log4jVersion = '1.2.17'
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/Alvearie/hri-flink-pipeline-core")
            credentials {
                username = findProperty('USER') ?: System.getenv('USER')
                password = findProperty('PASSWORD') ?: System.getenv('PASSWORD')
            }
        }
        mavenCentral()
        mavenLocal()
    }

    ext {
        branch = System.getenv('BRANCH_NAME') != null
                ? System.getenv('BRANCH_NAME')
                : getWorkingBranch()
    }

    // If not running in travis add 'local' to the version to support local development
    if (System.getenv('BRANCH_NAME') == null || System.getenv('BRANCH_NAME') == "") {
        version = "${branch}-local-SNAPSHOT"
    } else if (System.getenv('ACTIONS_TAG') == null || System.getenv('ACTIONS_TAG') == "") {
        version = "${branch}-SNAPSHOT"
    } else if (System.getenv('ACTIONS_TAG') == "v${version}") {
        version = "${version}"
    } else {
        throw new InvalidUserDataException(String.format("The tag '%s' does not match with the current release version '%s'",System.getenv('ACTIONS_TAG'),"${version}"));
    }
}

dependencies {
    // Scala lib
    implementation "org.scala-lang:scala-library:${scalaVersion}"
}

// prevents parent project from producing build output
jar.enabled = false

/**
 * Get the name of the working branch of the project
 *
 * @return Name of the working branch
 */
def getWorkingBranch() {
    // Triple double-quotes for the breaklines
    def workingBranch = """git --git-dir=${rootDir}/.git
                               --work-tree=${rootDir}
                               rev-parse --abbrev-ref HEAD""".execute().text.trim()
    println "Working branch: " + workingBranch
    return workingBranch
}

